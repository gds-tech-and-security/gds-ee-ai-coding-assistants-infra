data "aws_iam_policy_document" "deny_insecure_transport" {
  count = var.deny_insecure_transport ? 1 : 0

  statement {
    sid    = "denyInsecureTransport"
    effect = "Deny"

    actions = [
      "s3:*",
    ]

    resources = [
      aws_s3_bucket.this.arn,
      "${aws_s3_bucket.this.arn}/*",
    ]

    principals {
      type        = "*"
      identifiers = ["*"]
    }

    condition {
      test     = "Bool"
      variable = "aws:SecureTransport"
      values = [
        "false"
      ]
    }
  }
}

data "aws_iam_policy_document" "require_latest_tls" {
  count = var.minimum_tls_version != "" ? 1 : 0

  statement {
    sid    = "denyOutdatedTLS"
    effect = "Deny"

    actions = [
      "s3:*",
    ]

    resources = [
      aws_s3_bucket.this.arn,
      "${aws_s3_bucket.this.arn}/*",
    ]

    principals {
      type        = "*"
      identifiers = ["*"]
    }

    condition {
      test     = "NumericLessThan"
      variable = "s3:TlsVersion"
      values = [
        var.minimum_tls_version
      ]
    }
  }
}

data "aws_iam_policy_document" "combined" {
  count = var.attach_policy ? 1 : 0

  source_policy_documents = compact([
    var.minimum_tls_version != "" ? data.aws_iam_policy_document.require_latest_tls[0].json : "",
    var.deny_insecure_transport ? data.aws_iam_policy_document.deny_insecure_transport[0].json : "",
    var.policy != null ? var.policy : ""
  ])
}

resource "aws_s3_bucket_policy" "this" {
  count = var.attach_policy ? 1 : 0

  bucket = aws_s3_bucket.this.id
  policy = data.aws_iam_policy_document.combined[0].json

  depends_on = [
    aws_s3_bucket_public_access_block.this
  ]
}
